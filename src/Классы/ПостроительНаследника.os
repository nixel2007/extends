#Использовать annotations
#Использовать decorator
#Использовать reflector

Перем _ОбъектНаследник;
Перем _ОбъектРодитель;

Функция Построить() Экспорт

	РефлекторОбъекта = Новый РефлекторОбъекта(_ОбъектНаследник);
	
	МетодыНаследника = РефлекторОбъекта.ПолучитьТаблицуМетодов(, Ложь);
	Метод = РаботаСАннотациями.НайтиМетодыСАннотацией(МетодыНаследника, "Наследует")[0];
	АннотацияНаследует = РаботаСАннотациями.НайтиАннотацию(Метод.Аннотации, "Наследует");
	
	ИмяТипаРодителя = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(АннотацияНаследует);
	РефлекторОбъектаРодителя = Новый РефлекторОбъекта(Тип(ИмяТипаРодителя));
	МетодыРодителя = РефлекторОбъектаРодителя.ПолучитьТаблицуМетодов();

	Декоратор = Новый ПостроительДекоратора(_ОбъектНаследник);

	// Переделать на поиск поля с аннотацией &Родитель
	Если РефлекторОбъекта.ЕстьСвойство("_ОбъектРодитель") Тогда
		Рефлектор = Новый Рефлектор();
		Рефлектор.УстановитьСвойство(_ОбъектНаследник, "_ОбъектРодитель", _ОбъектРодитель);
	Иначе
		Поле = Новый Поле("_ОбъектРодитель")
			.ЗначениеПоУмолчанию(_ОбъектРодитель);
	
		Декоратор.Поле(Поле);
	КонецЕсли;
	
	Для Каждого МетодРодителя Из МетодыРодителя Цикл
		
		Если МетодРодителя.ЭтоФункция И РефлекторОбъекта.ЕстьФункция(МетодРодителя.Имя, МетодРодителя.КоличествоПараметров) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ МетодРодителя.ЭтоФункция И РефлекторОбъекта.ЕстьПроцедура(МетодРодителя.Имя, МетодРодителя.КоличествоПараметров) Тогда
			Продолжить;
		КонецЕсли;

		Метод = Новый Метод(МетодРодителя.Имя).Публичный();
		
		ТелоМетода = "_ОбъектРодитель." + МетодРодителя.Имя + "();";
		Если МетодРодителя.ЭтоФункция Тогда
			ТелоМетода = "Возврат " + ТелоМетода;
		Иначе
			Метод.ЭтоПроцедура();
		КонецЕсли;
		Метод.ТелоМетода(ТелоМетода);

		Декоратор.Метод(Метод);

	КонецЦикла;

	Объект = Декоратор.Построить();

	Возврат Объект;
КонецФункции

Процедура ПриСозданииОбъекта(ОбъектНаследник, ОбъектРодитель)
	_ОбъектНаследник = ОбъектНаследник;
	_ОбъектРодитель = ОбъектРодитель;
КонецПроцедуры
